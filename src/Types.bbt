REM >!Types
REM
REM Copyright 2020, Stephen Fryatt (info@stevefryatt.org.uk)
REM
REM This file is part of Clipper:
REM
REM   http://www.stevefryatt.org.uk/risc-os
REM
REM Licensed under the EUPL, Version 1.2 only (the "Licence");
REM You may not use this work except in compliance with the
REM Licence.
REM
REM You may obtain a copy of the Licence at:
REM
REM   http://joinup.ec.europa.eu/software/page/eupl
REM
REM Unless required by applicable law or agreed to in
REM writing, software distributed under the Licence is
REM distributed on an "AS IS" basis, WITHOUT WARRANTIES
REM OR CONDITIONS OF ANY KIND, either express or implied.
REM
REM See the Licence for the specific language governing
REM permissions and limitations under the Licence.


REM Find a file icon sprite for a numeric filetype, falling back
REM to file_xxx if a suitable sprite isn't available.
REM
REM \param filetype%	The filetype to look up.
REM \return		The sprite name, in the Wimp pool.
:
DEF FNtypes_sprite_from_type(filetype%)
LOCAL sprite$, flags%

REM Discard out of bounds types immediately.

IF filetype% < 0 OR filetype% > &FFF THEN = "file_xxx"

REM Try the type's sprite, and fall back if it doesn't exist.

sprite$ = "file_" + RIGHT$("000" + STR$~(filetype%), 3)

SYS "XWimp_SpriteOp", 40,,sprite$ TO ;flags%
IF flags% AND 1 THEN sprite$ = "file_xxx"

=sprite$


REM Find the textual name of a filetype from its numeric value.
REM
REM \param filetype%	The filetype to look up.
REM \return		The textual name.
:
DEF FNtypes_name_from_type(filetype%)
LOCAL hex$, len%, flags%

IF filetype% < 0 OR filetype% > &FFF THEN = "-"

hex$ = STR$~(filetype%)

SYS "XOS_ReadVarVal", "File$Type_"+hex$, q%, 256, 0, 3 TO ,,len%; flags%
IF flags% AND 1 THEN =hex$

q%?len% = 0
=FNstring_read(q%)
