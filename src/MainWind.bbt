

REM Load the main window template and initialise the variables.
:
DEF PROCmain_wind_initialise
PROCtemplate_load_claim("Main", b%, 1024, -1)
SYS "Wimp_CreateWindow",,b% TO MainWindow%

REM The icon handles.

FileSprIcon% = 0
FileTypeIcon% = 2
FileNameIcon% = 3
OKIcon% = 4
OwnerIcon% = 6
MainWindProtoIcon% = 7

ENDPROC



REM Close the main window.
:
DEF PROCmain_wind_close
PROCwindow_close(MainWindow%)
ENDPROC




DEF PROCmain_wind_set_content(filename$, taskname$, filetype%)
LOCAL sprite$

sprite$ = FNclipboard_sprite_from_type(filetype%)

PROCicon_set_indirected_text(MainWindow%, FileNameIcon%, filename$)
PROCicon_set_indirected_text(MainWindow%, OwnerIcon%, taskname$)
PROCicon_set_indirected_text(MainWindow%, FileTypeIcon%, FNclipboard_name_from_type(filetype%))

PROCset_ind(FileSprIcon%, "S" + sprite$ + ";R2")

SYS "Wimp_GetCaretPosition",,q%
IF q%!0 = MainWindow% THEN PROCicon_put_caret(q%!0, q%!4, q%!20)

PROCicon_set_shaded(MainWindow%, FileNameIcon%, FALSE)
PROCicon_set_shaded(MainWindow%, FileSprIcon%, FALSE)
PROCicon_set_shaded(MainWindow%, OKIcon%, FALSE)
PROCicon_set_shaded(MainWindow%, OwnerIcon%, FALSE)
PROCicon_set_shaded(MainWindow%, FileTypeIcon%, FALSE)
ENDPROC


REM Set the protocol message field to an error message, and enlarge the
REM window to show it.
REM
REM \param token$	The error message token to display
:
DEF PROCmain_wind_set_proto_error(token$)
LOCAL background%, message$

message$ = FNmessage_lookup(token$)
PROCicon_set_indirected_text(MainWindow%, MainWindProtoIcon%, message$)

background% = FNicon_background_colour(MainWindow%, MainWindProtoIcon%)
PROCicon_set_colours(MainWindow%, MainWindProtoIcon%, 11, background%)

REM If the window is already open, bring it to the front and enlarge it
REM to show the protocol message field.

q%!0 = MainWindow%
SYS "Wimp_GetWindowState",, q%

IF q%!32 AND (1 << 16) THEN
	q%!8 -= 128
	q%!28 = -1
	SYS "Wimp_OpenWindow",, q%
ENDIF
ENDPROC


REM Set the protocol message field to a standard message.
REM
REM \param token$	The message token to display
:
DEF PROCmain_wind_set_proto_message(token$)
LOCAL background%, message$

message$ = FNmessage_lookup(token$)
PROCicon_set_indirected_text(MainWindow%, MainWindProtoIcon%, message$)

background% = FNicon_background_colour(MainWindow%, MainWindProtoIcon%)
PROCicon_set_colours(MainWindow%, MainWindProtoIcon%, 7, background%)
ENDPROC
