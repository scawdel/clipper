

REM Load the main window template and initialise the variables.
:
DEF PROCmain_wind_initialise
PROCtemplate_load_claim("Main", b%, 1024, -1)
SYS "Wimp_CreateWindow",,b% TO MainWindow%

REM The icon handles.

MainWindFileSprIcon% = 0
MainWindFileTypeIcon% = 2
MainWindFileNameIcon% = 3
MainWindOKIcon% = 4
MainWindOwnerIcon% = 6
MainWindProtoIcon% = 7
ENDPROC


REM Close the main window.
:
DEF PROCmain_wind_close
PROCwindow_close(MainWindow%)
ENDPROC


REM Set the content fields to reflect the current clipboard content.
REM
REM \param filename$	The filename of the content.
REM \param taskname$	The name of the task owning the content.
REM \param filetype%	The filetype of the content, or -1 for none.
:
DEF PROCmain_wind_set_content(filename$, taskname$, filetype%)
LOCAL sprite$

sprite$ = FNclipboard_sprite_from_type(filetype%)

PROCicon_set_indirected_text(MainWindow%, MainWindFileNameIcon%, filename$)
PROCicon_set_indirected_text(MainWindow%, MainWindOwnerIcon%, taskname$)
PROCicon_set_indirected_text(MainWindow%, MainWindFileTypeIcon%, FNclipboard_name_from_type(filetype%))

PROCset_ind(MainWindFileSprIcon%, "S" + sprite$ + ";R2")

SYS "Wimp_GetCaretPosition",,q%
IF q%!0 = MainWindow% THEN PROCicon_put_caret(q%!0, q%!4, q%!20)

PROCicon_set_shaded(MainWindow%, MainWindFileNameIcon%, FALSE)
PROCicon_set_shaded(MainWindow%, MainWindFileSprIcon%, FALSE)
PROCicon_set_shaded(MainWindow%, MainWindOKIcon%, FALSE)
PROCicon_set_shaded(MainWindow%, MainWindOwnerIcon%, FALSE)
PROCicon_set_shaded(MainWindow%, MainWindFileTypeIcon%, FALSE)
ENDPROC


REM Get the current filename from the filename field.
REM
REM \return		The filename in the field.
:
DEF FNmain_wind_get_filename
=FNicon_get_text(MainWindow%, MainWindFileNameIcon%)


REM Set the protocol message field to an error message, and enlarge the
REM window to show it.
REM
REM \param token$	The error message token to display
:
DEF PROCmain_wind_set_proto_error(token$)
LOCAL background%, message$

message$ = FNmessage_lookup(token$)
PROCicon_set_indirected_text(MainWindow%, MainWindProtoIcon%, message$)

background% = FNicon_background_colour(MainWindow%, MainWindProtoIcon%)
PROCicon_set_colours(MainWindow%, MainWindProtoIcon%, 11, background%)

REM If the window is already open, bring it to the front and enlarge it
REM to show the protocol message field.

q%!0 = MainWindow%
SYS "Wimp_GetWindowState",, q%

IF q%!32 AND (1 << 16) THEN
	q%!8 -= 128
	q%!28 = -1
	SYS "Wimp_OpenWindow",, q%
ENDIF
ENDPROC


REM Set the protocol message field to a standard message.
REM
REM \param token$	The message token to display
:
DEF PROCmain_wind_set_proto_message(token$)
LOCAL background%, message$

message$ = FNmessage_lookup(token$)
PROCicon_set_indirected_text(MainWindow%, MainWindProtoIcon%, message$)

background% = FNicon_background_colour(MainWindow%, MainWindProtoIcon%)
PROCicon_set_colours(MainWindow%, MainWindProtoIcon%, 7, background%)
ENDPROC
